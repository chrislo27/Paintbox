import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinCompilationTask

buildscript {
    repositories {
        gradlePluginPortal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "kotlin"
    apply plugin: "maven-publish"

    ext {
        appName = "Paintbox"
        gdxVersion = '1.12.0'
        gdxControllersVersion = '2.2.2'
        paintboxVersion = "0.10.0"
    }
    version = ext.paintboxVersion
    archivesBaseName = "Paintbox-${project.name}"
    
    java {
        toolchain.languageVersion.set(JavaLanguageVersion.of(11))
        withSourcesJar()
        withJavadocJar()
    }

    repositories {
        mavenCentral()
//        maven { url 'https://jitpack.io/' }
    }

    tasks.withType(KotlinCompilationTask).configureEach {
        compilerOptions {
            jvmTarget.set(JvmTarget.JVM_11)
            freeCompilerArgs.add("-opt-in=kotlin.ExperimentalUnsignedTypes")
            freeCompilerArgs.add("-opt-in=kotlin.RequiresOptIn")
        }
    }

    if (System.getenv("JITPACK") == "true") {
        // Jitpack generates the wrong Gradle .module data and causes missing native dependencies (classifiers).
        // https://github.com/jitpack/jitpack.io/issues/4476
        tasks.withType(GenerateModuleMetadata).configureEach {
            enabled = false
        }
    }
}

project(":utils") {
    dependencies {
        api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        api "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

        api "com.eclipsesource.minimal-json:minimal-json:0.9.5"
        api "com.beust:jcommander:1.82"
        api "org.slf4j:slf4j-simple:1.7.33"
        api "io.dropwizard.metrics:metrics-core:4.2.7"
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId = 'com.github.chrislo27'
                artifactId = 'paintbox-utils'

                from components.java
            }
        }
    }
}

project(":core") {
    dependencies {
        api project(":utils")
        
        api "com.badlogicgames.gdx:gdx:$gdxVersion"
        api "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
        api "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
//        api "com.badlogicgames.gdx-controllers:gdx-controllers-core:$gdxControllersVersion"
        api "org.lwjgl:lwjgl-tinyfd:$lwjgl_tinyfd_version"
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId = 'com.github.chrislo27'
                artifactId = 'paintbox-core'

                from components.java
            }
        }
    }
}

project(":desktop") {
    dependencies {
        api project(":core")

        api "org.lwjgl:lwjgl-tinyfd:$lwjgl_tinyfd_version:natives-windows"
        api "org.lwjgl:lwjgl-tinyfd:$lwjgl_tinyfd_version:natives-windows-x86"
        api "org.lwjgl:lwjgl-tinyfd:$lwjgl_tinyfd_version:natives-windows-arm64"
        api "org.lwjgl:lwjgl-tinyfd:$lwjgl_tinyfd_version:natives-linux"
        api "org.lwjgl:lwjgl-tinyfd:$lwjgl_tinyfd_version:natives-linux-arm64"
        api "org.lwjgl:lwjgl-tinyfd:$lwjgl_tinyfd_version:natives-linux-arm32"
        api "org.lwjgl:lwjgl-tinyfd:$lwjgl_tinyfd_version:natives-macos"
        api "org.lwjgl:lwjgl-tinyfd:$lwjgl_tinyfd_version:natives-macos-arm64"

        api "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        api "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
//        api "com.badlogicgames.gdx:gdx-lwjgl3-glfw-awt-macos:$gdxVersion"
//        implementation "com.badlogicgames.gdx-controllers:gdx-controllers-desktop:$gdxControllersVersion"
    }
    
    publishing {
        publications {
            maven(MavenPublication) {
                groupId = 'com.github.chrislo27'
                artifactId = 'paintbox-desktop'

                from components.java
            }
        }
    }
}