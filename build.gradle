import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinCompilationTask

buildscript {
    repositories {
        gradlePluginPortal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "kotlin"
    apply plugin: "maven-publish"

    ext {
        appName = "Paintbox"
        gdxVersion = '1.13.1'
        gdxControllersVersion = '2.2.3'
        paintboxVersion = "1.0.0"
    }
    version = ext.paintboxVersion
    archivesBaseName = "Paintbox-${project.name}"
    
    java {
        toolchain.languageVersion.set(JavaLanguageVersion.of(21))
        withSourcesJar()
        withJavadocJar()
        javadoc.options.addStringOption('Xdoclint:none', '-quiet')
    }

    repositories {
        mavenCentral()
//        maven { url 'https://jitpack.io/' }
    }
    
    dependencies {
        testImplementation "org.jetbrains.kotlin:kotlin-test"
        testImplementation "org.hamcrest:hamcrest:2.2"
    }

    test {
        useJUnitPlatform()
    }

    tasks.withType(KotlinCompilationTask).configureEach {
        compilerOptions {
            jvmTarget.set(JvmTarget.JVM_21)
            freeCompilerArgs.add("-opt-in=kotlin.ExperimentalUnsignedTypes")
            freeCompilerArgs.add("-opt-in=kotlin.RequiresOptIn")
        }
    }

    if (System.getenv("JITPACK") == "true") {
        // Jitpack generates the wrong Gradle .module data and causes missing native dependencies (classifiers).
        // https://github.com/jitpack/jitpack.io/issues/4476
        tasks.withType(GenerateModuleMetadata).configureEach {
            enabled = false
        }
    }
}

project(":binding") {
    dependencies {
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId = 'com.github.chrislo27'
                artifactId = 'paintbox-binding'

                from components.java
            }
        }
    }
}

project(":utils") {
    dependencies {
        api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        api "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

        implementation "org.slf4j:slf4j-simple:2.0.16"
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId = 'com.github.chrislo27'
                artifactId = 'paintbox-utils'

                from components.java
            }
        }
    }
}

project(":core") {
    dependencies {
        api project(":utils")
        api project(":binding")
        
        api "com.badlogicgames.gdx:gdx:$gdxVersion"
        api "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
        api "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
        api "org.lwjgl:lwjgl-tinyfd:$lwjgl_version"
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId = 'com.github.chrislo27'
                artifactId = 'paintbox-core'

                from components.java
            }
        }
    }
}

project(":ui") {
    dependencies {
        api project(":core")
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId = 'com.github.chrislo27'
                artifactId = 'paintbox-ui'

                from components.java
            }
        }
    }
}

project(":desktop") {
    dependencies {
        api project(":core")
        testApi project(":ui")

        api "org.lwjgl:lwjgl-tinyfd:$lwjgl_version:natives-windows"
        api "org.lwjgl:lwjgl-tinyfd:$lwjgl_version:natives-windows-x86"
        api "org.lwjgl:lwjgl-tinyfd:$lwjgl_version:natives-windows-arm64"
        api "org.lwjgl:lwjgl-tinyfd:$lwjgl_version:natives-linux"
        api "org.lwjgl:lwjgl-tinyfd:$lwjgl_version:natives-linux-arm64"
        api "org.lwjgl:lwjgl-tinyfd:$lwjgl_version:natives-linux-arm32"
        api "org.lwjgl:lwjgl-tinyfd:$lwjgl_version:natives-macos"
        api "org.lwjgl:lwjgl-tinyfd:$lwjgl_version:natives-macos-arm64"

        api "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        api "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"

        api "org.jcommander:jcommander:2.0"
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId = 'com.github.chrislo27'
                artifactId = 'paintbox-desktop'

                from components.java
            }
        }
    }
}